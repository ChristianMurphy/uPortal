description = 'Apereo uPortal Search API'

configurations {
    jaxb
}

ext {
    generatedSourcesDir = "${buildDir}/generated-sources/xjc"
}

dependencies {
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.3.9'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.3.9'
    jaxb "javax.xml.bind:jaxb-api:${jaxbApiVersion}"
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:1.11.1'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:1.11.1'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.1.0'
    api "javax.xml.bind:jaxb-api:${jaxbApiVersion}"
}

/*
 * This section is the key to IDE integration.  IDE will look for source files in both...
 *
 *   - src/main/java
 *   - build/generated-sources/xjc
 */
sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir generatedSourcesDir
        }
    }
}

task xjc {
    inputs.dir "${projectDir}/src/main/resources/xsd"
    inputs.dir "${projectDir}/src/main/binding"
    outputs.dir generatedSourcesDir

    doLast {
        System.setProperty('javax.xml.accessExternalSchema', 'all')
        mkdir generatedSourcesDir
        ant.taskdef(
            name: 'xjc',
            classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task',
            classpath: configurations.jaxb.asPath
        )
        ant.xjc(
            destdir: generatedSourcesDir,
            extension: 'true'
        ) {
            schema(dir: "${projectDir}/src/main/resources/xsd", includes: '**/*.xsd')
            binding(dir: "${projectDir}/src/main/binding", includes: 'bindings.xjb')
            classpath { path(path: configurations.jaxb.asPath) }
        }
    }
}

compileJava.dependsOn(xjc)
